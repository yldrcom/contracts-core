// SPDX-License-Identifier: MIT
pragma solidity ^0.8.10;

import {ReserveConfiguration} from "../../libraries/configuration/ReserveConfiguration.sol";
import {Math} from "@openzeppelin/contracts/utils/math/Math.sol";
import {FixedPoint128} from "@uniswap/v3-core/contracts/libraries/FixedPoint128.sol";

/// Generic adapter to UniV3-like concentrated liquidity DEX. Currently oriented
/// and has implementations mainly for Uniswap V3 and Algebra contracts, but can
/// also be extended to support other DEXs with similar semantics.
abstract contract BaseCLAdapter {
    struct PositionData {
        uint256 tokenId;
        address token0;
        address token1;
        // Kept for compatibility, should be set to 0 for protocol with variable pool fees (e.g. Algebra)
        uint24 fee;
        uint128 liquidity;
        int24 tickLower;
        int24 tickUpper;
        uint128 tokensOwed0;
        uint128 tokensOwed1;
        uint256 feeGrowthInside0LastX128;
        uint256 feeGrowthInside1LastX128;
    }

    struct MintParams {
        address token0;
        address token1;
        // Kept for compatibility, should be set to 0 for protocol with variable pool fees (e.g. Algebra)
        uint24 fee;
        int24 tickLower;
        int24 tickUpper;
        uint256 amount0Desired;
        uint256 amount1Desired;
        uint256 amount0Min;
        uint256 amount1Min;
        address recipient;
        uint256 deadline;
    }

    function _getPositionData(uint256 tokenId) internal view virtual returns (PositionData memory);

    function _collectFees(uint256 tokenId, uint128 amount0Max, uint128 amount1Max, address receiver)
        internal
        virtual
        returns (uint256 amount0, uint256 amount1);

    function _increaseLiquidity(uint256 tokenId, uint256 amount0, uint256 amount1)
        internal
        virtual
        returns (uint256 amount0Resulted, uint256 amount1Resulted);

    function _decreaseLiquidity(uint256 tokenId, uint128 liquidity)
        internal
        virtual
        returns (uint256 amount0, uint256 amount1);

    function _getPool(PositionData memory position) internal view virtual returns (address);

    function _getPositionManager() internal view virtual returns (address);

    function _getFeeGrowths(address pool, int24 tick)
        internal
        view
        virtual
        returns (uint256 feeGrowthOutside0X128, uint256 feeGrowthOutside1X128);

    function _getPoolState(address pool) internal view virtual returns (uint160 sqrtPriceX96, int24 tick);

    function _getPoolLiquidity(address pool) internal view virtual returns (uint128 liquidity);

    function _getGlobalFeeGrowths(address pool)
        internal
        view
        virtual
        returns (uint256 feeGrowthGlobal0X128, uint256 feeGrowthGlobal1X128);

    function _mintPosition(MintParams memory params)
        internal
        virtual
        returns (uint256 tokenId, uint128 liquidity, uint256 amount0, uint256 amount1);

    /// @dev Logic is taken from uniswap-v3/core/contracts/libraries/Tick.sol
    function _getFeeGrowthInside(address pool, int24 tickLower, int24 tickUpper)
        private
        view
        returns (uint256 feeGrowthInside0X128, uint256 feeGrowthInside1X128)
    {
        unchecked {
            (, int24 tickCurrent) = _getPoolState(pool);

            (uint256 feeGrowthGlobal0X128, uint256 feeGrowthGlobal1X128) = _getGlobalFeeGrowths(pool);

            (uint256 feeGrowthOutsideLower0X128, uint256 feeGrowthOutsideLower1X128) = _getFeeGrowths(pool, tickLower);
            (uint256 feeGrowthOutsideUpper0X128, uint256 feeGrowthOutsideUpper1X128) = _getFeeGrowths(pool, tickUpper);

            // calculate fee growth below
            uint256 feeGrowthBelow0X128;
            uint256 feeGrowthBelow1X128;
            if (tickCurrent >= tickLower) {
                feeGrowthBelow0X128 = feeGrowthOutsideLower0X128;
                feeGrowthBelow1X128 = feeGrowthOutsideLower1X128;
            } else {
                feeGrowthBelow0X128 = feeGrowthGlobal0X128 - feeGrowthOutsideLower0X128;
                feeGrowthBelow1X128 = feeGrowthGlobal1X128 - feeGrowthOutsideLower1X128;
            }

            // calculate fee growth above
            uint256 feeGrowthAbove0X128;
            uint256 feeGrowthAbove1X128;
            if (tickCurrent < tickUpper) {
                feeGrowthAbove0X128 = feeGrowthOutsideUpper0X128;
                feeGrowthAbove1X128 = feeGrowthOutsideUpper1X128;
            } else {
                feeGrowthAbove0X128 = feeGrowthGlobal0X128 - feeGrowthOutsideUpper0X128;
                feeGrowthAbove1X128 = feeGrowthGlobal1X128 - feeGrowthOutsideUpper1X128;
            }

            feeGrowthInside0X128 = feeGrowthGlobal0X128 - feeGrowthBelow0X128 - feeGrowthAbove0X128;
            feeGrowthInside1X128 = feeGrowthGlobal1X128 - feeGrowthBelow1X128 - feeGrowthAbove1X128;
        }
    }

    function _getPendingFees(PositionData memory position) internal view returns (uint256 amount0, uint256 amount1) {
        // Non-zero positions can't have unclaimed fees by design of position manager
        if (position.liquidity == 0) {
            return (position.tokensOwed0, position.tokensOwed1);
        }

        (uint256 feeGrowthInside0X128, uint256 feeGrowthInside1X128) =
            _getFeeGrowthInside(_getPool(position), position.tickLower, position.tickUpper);
        unchecked {
            // overflow in fee growth subtraction is expected
            amount0 = position.tokensOwed0
                + uint128(
                    Math.mulDiv(
                        feeGrowthInside0X128 - position.feeGrowthInside0LastX128, position.liquidity, FixedPoint128.Q128
                    )
                );
            amount1 = position.tokensOwed1
                + uint128(
                    Math.mulDiv(
                        feeGrowthInside1X128 - position.feeGrowthInside1LastX128, position.liquidity, FixedPoint128.Q128
                    )
                );
        }
    }
}
